
#include "interfaces/Map_Interface.hps"
#include "base/AchievementHandler_Types.hps"
#include "base/InputHandler_Types.hps"
#include "base/RichPresenceHandler_Types.hps"

#include "helpers/helper_game.hps"
#include "helpers/helper_effects.hps"
#include "helpers/helper_map.hps"
#include "helpers/helper_props.hps"
#include "helpers/helper_audio.hps"
#include "helpers/helper_sequences.hps"
#include "helpers/helper_modules.hps"
#include "helpers/helper_imgui.hps"
#include "helpers/helper_areas.hps"
#include "helpers/helper_ai.hps"
#include "helpers/helper_player.hps"

#include "helper_imgui_station.hps"
#include "helper_imgui_station_app_error.hps"
#include "helper_imgui_station_app_audioplayback.hps"
#include "helper_imgui_station_app_photoviewer.hps"
#include "helper_imgui_cath_portrait.hps"
#include "helper_imgui_station_basics.hps"

#include "helper_custom_depth.hps"
#include "helper_custom_depth_ai.hps"
#include "helper_custom_depth_ai_fleshers.hps"
#include "helper_custom_depth_imgui.hps"
#include "helper_custom_depth_audio.hps"

#include "custom_depth/helper_custom_depth_audio.hps"

#include "helper_font.hps"

#include "player/Player_Types.hps"

//--------------------------------------------------
 
/*Place any global values here. These must be const variables as they will not be saved*/
/*This is also the place for enums and classes, but these should be avoided whenever possible*/

//--------------------------------------------------

class cScrMap : iScrMap
{
	//--------------------------------------------
 
	//////////////////////////////////////////////////////////////////////////////////////////
	// ==============
	// MAIN CALLBACKS
	// ==============
	//{///////////////////////////////////////////////////////////////////////////////////////
 
	//-------------------------------------------------------
	
	//-------------------------------------------------------
 
	////////////////////////////
	// Set up map environment
	void Setup()
	{

	}
	
	//-------------------------------------------------------

	void PreloadData()
	{
		PlayerHands_PreloadHandModel_Human();
	}
	
	//-------------------------------------------------------

	////////////////////////////
	// Run first time starting map
	void OnStart()
	{
		/////////////////////////
		// Player setup
		Player_SetAmbientLight_Indoors();
		PlayerHands_SetHandModel_DeepSea();
		
		Player_SetFlashlightDisabled(true);
		
		/////////////////////////
		// Level setup
		//Map_Preset_Fade("Ocean", 0.001f);
		
		bool bDoIntro = true;
		////////////////////
		//Audio
		
		/////////////////////////
		// Debug
		if(cLux_ScriptDebugOn()) 
		{
			bDoIntro = true;
			
			Player_SetFlashlightDisabled(false);
			
			//Effect_Fade_In(1);
			//Sound_FadeGlobalVolume(1.0f, 1.0f);
			//Sound_FadeGlobalVolume(1.0f, 1.0f, eSoundEntryType_GuiAll);
		}
		
		if (bDoIntro)
		{
			Effect_Fade_Out(0);
			Sound_SetGlobalVolume(0.0f, eSoundEntryType_WorldAll);
			Sound_SetGlobalVolume(0.0f, eSoundEntryType_GuiWorld);
			
			Map_AddTimer("timer_kickoffintro", 0.01, "TimerKickoffIntro");
		}
		
		Map_AddTimer("preload", 4, "TimerBeginPreload");
	}

	//-------------------------------------------------------

	////////////////////////////
	// Run when entering map
	void OnEnter()
	{
		
	}

	//-------------------------------------------------------

	////////////////////////////
	// Run when leaving map
	void OnLeave()
	{
		if(mlImageTrailID>=0)
		{
			cLux_AddDebugMessage("FadeOutImageTrail!");
			Effect_ImageTrail_FadeOut(mlImageTrailID,10.0f);
			mlImageTrailID=-1;
		}
	}
	
	//-------------------------------------------------------
	
	float mfHeartBeatTimer = 0; // Value gets incremented by afTimeStep untill reached mfTimeBetweenBeats
	
	void Update(float afTimeStep)
	{
		// Stress tracker
		//cLux_AddDebugMessage("Time between beats: " + mfTimeBetweenBeats);
		//cLux_AddDebugMessage("Time left untill beep: " +  (mfTimeBetweenBeats-mfHeartBeatTimer));
		
		if (mbStressTrackerActive)
		{
			mfHeartBeatTimer+=afTimeStep;
			
			if (mfHeartBeatTimer >= mfTimeBetweenBeats)
			{
				if (!Sound_Exists("HeartBeepSound"))
				{
					Sound_CreateAtEntity("HeartBeepSound", "Entities_Station/tech/life_support/heart_beep", "StressHeartBeepSource", 0.0f, false, 0.5f);
					mfHeartBeatTimer = 0;
				}
			}
		}
	}

	//-------------------------------------------------------

	////////////////////////////
	// The player has died.
	void OnPlayerKilled(int alRecentDeaths, const tString&in asSource)
	{
	}

	//-------------------------------------------------------

	////////////////////////////
	// To get when player makes input (mostly used for debug)
	void OnAction(int alAction, bool abPressed) 
	{
		if(abPressed==false) return;
		
		if(alAction == eAction_Test1)
		{
			// SCREEN EFFECTS FOR WHEN PLAYER WAKES UP
		}
	}

	//-------------------------------------------------------

	////////////////////////////
	// This only used for pure debug purposes when info needs to printed.
	float DrawDebugOutput(cGuiSet @apSet,iFontData @apFont,float afY)
	{
		//afY = cLux_DrawDebugText("My Debug value:"+..., afY);
		return afY;
	}
	
	//-------------------------------------------------------
	
	void OnGui(float afTimeStep)
	{
		// ADD SO CURRENT POWERSTAGE ID IS PRINTED ON SCREEN -- MOVE TO ONGUI FUNC
		// ADD SO THE TEXT SAYS "On", "Off", "Unreliable", "Flicker" DEPENDING ON THE STATE
	}
 
	//} END MAIN CALLBACKS
 
	//////////////////////////////////////////////////////////////////////////////////////////
	// ==============
	// MAIN FUNCTIONS
	// ==============
	//{///////////////////////////////////////////////////////////////////////////////////////
	
	float mfPlayerSpeedOnWakeup = 1.0f;
	
	int mlImageTrailID = -1;
	int mlDOFLightsOn = -1;
	
	void Timer_RemoveImageTrail(const tString&in asTimer)
	{
		if(mlImageTrailID>=0)
		{
			cLux_AddDebugMessage("FadeOutImageTrail!");
			Effect_ImageTrail_FadeOut(mlImageTrailID,10.0f);
			mlImageTrailID=-1;
		}
	}
	
	//-------------------------------------------------------
	
	///////////////////////////////
	// Heart stress
	///////////////////////////////
	
	int mlHeartBeatID = -1;
	float mfTimeBetweenBeats;
	bool mbStressTrackerActive = false;
	int StressHeartBeep(float afTimeBetweenBeats, float afVolume, int alPrio, 
						 float afDuration = -1, bool abPlayBeep = true, float afFadeInTime = 3.0f, float afFadeOutTime = 3.0f)
	{
		mlHeartBeatID = Emotion_StartHeartbeat(afTimeBetweenBeats, afVolume, alPrio, afDuration, afFadeInTime, afFadeOutTime);
		mbStressTrackerActive = abPlayBeep;
		mfTimeBetweenBeats = afTimeBetweenBeats;
		
		return mlHeartBeatID;
	}
		
	//-------------------------------------------------------
		
	void HeartBeatSound(const tString &in asTimer)
	{
		Sound_CreateAtEntity("HeartBeepSound", "Entities_Station/tech/life_support/heart_beep", "StressHeartBeepSource", 0.0f, false, 0.5f);
		Map_RestartCurrentTimer(mfTimeBetweenBeats);
	}
		
	///////////////////////////////
	// Checkpoints
	///////////////////////////////
	
	//-------------------------------------------------------
	
	//} END MAIN FUNCTIONS
 
	/////////////////////////////////////////////////////////////////////////////////////////
	// ==============
	// SCENE 1 *INTRO*
	// ==============
	//{//////////////////////////////////////////////////////////////////////////////////////
 
		/////////////////////////////////////////
		// *Brandon wakes up in the scan chair*
		//{//////////////////////////////////////
		
		int mlCurrentImageTrailEffectID = 0;
		int mlDoFEffectID = 0;
		int mlRadialBlurID = 0;
		
		tString msVoidBrandon = "Brandon_Void";
		
		tString msCurrentSequence;
		tString msSimBrandon = "Brandon_ScanRoom";
		tString msSimAlice = "Alice_ScanRoom";
		tString msCurrentCathSource = "ChunVoid";
		
		void TimerKickoffIntro(const tString &in asTimer)
		{
			SetupIntro();
		}
		
		//-------------------------------------------------------
		
		void SetupIntro()
		{
			Player_Teleport("PlayerStartArea_1", true);
			
			///////////////////
			// Scan room
			Entity_SetActive(msSimBrandon, true);
			Entity_SetActive(msSimAlice, true);
			
			NPC_SetMainAnimation(msSimBrandon, "sit_confused");
			
			Light_SetVisible("ScannerLight_*", false);
			
			Voice_SetSource("Wan", msSimBrandon, 1000, 1000, true, 1000);
			Voice_SetSource("Alice", msSimAlice, 1000, 1000, true, 1000);
			
			///////////////////
			// Visual setup
			Effect_Fade_Out(0.1);
			//Player_FadeFOVMulTo(0.75f, 0);
			Player_FadeFOVMulTo(1.75f, 25.0f);
			mlCurrentImageTrailEffectID = Effect_ImageTrail_Start(0.8f, 0, 1500.0f, 15.0f);
			mlDoFEffectID = Effect_DoF_Start(0.1f, 0.5f, 1, 0);
			
			mlRadialBlurID = Effect_RadialBlur_Start(0.15f, 0.25f, 0.35f, 0.5f);
			mlid_ScanLines = Effect_Screen_Start("00_04_scan_noise.mat", cVector2f(0.5, 0.5), cVector2f(2,1),  1, 0.1, 900, 0.1);
			
			///////////////////
			// Animation
			// MOVE TO PROPER STEP
			CameraAnimation_Begin("WakeupAnim", "", false);
					
			//////////////////////////////////////
			// Start intro after a short delay
			Map_AddTimer("", 0.1f, "SequenceScanRoom");
			
			mlImageTrailID = Effect_ImageTrail_Start(0.5f, 0, -1, 15.0f);
			Map_AddTimer("RemoveImageTrail", 20, "Timer_RemoveImageTrail");
		}
		
		//////////////////////////////////////////
		// SCAN ROOM SEQUENCE
		//////////////////////////////////////////
		
		//-------------------------------------------------------
		bool mbHelmetPhaseTwoStarted = false;
		//-------------------------------------------------------

		cSequenceStatesData mSequenceScanRoomState;
		void SequenceScanRoom(const tString &in asTimer)
		{
			Sequence_Begin("SequenceScanRoom", mSequenceScanRoomState);
			
			if (Sequence_DoStepAndWait(7.25f))
			{
				//////////////////////////////////
				// Wakeup sounds and audio effects
				Sound_PlayGui("01_01_upsilon_awake/sfx/wake_up/wake_up_event", 1.0f, eSoundEntryType_Gui);
				Sound_PlayGui("04_03_tau_escape/SFX/ross/conversation/tunnel/loop", 0.175f, eSoundEntryType_Gui);
				
				Sound_FadeGlobalVolume(1.0f, 1.0f, eSoundEntryType_Gui);
				PreloadPriorityNormal();
			}
			else if (Sequence_DoStepAndWait(2.0f))
			{
				///////////////////////////////
				// Switch to the stand up animation
				CameraAnimation_End();
				
				Music_PlayExt("01_01_awakendrone", true, 0.25f, 1.0f, 9.0f, 0.0f, eMusicPrio_MinorEvent, false);
				
				CameraAnimation_Begin("IntroAnim", "", true);
				CameraAnimationNode_SetInteractiveMovement("IntroAnim_1", false);
				CameraAnimationNode_SetInteractiveMovement("IntroAnim_2", false);
				
				Player_FadeMoveSpeedMulTo(mfPlayerSpeedOnWakeup,0.1f);
				
				/////////////////////////////
				// Fade out DOF effects
				Effect_DoF_FadeFalloff(mlDoFEffectID, 0.5, 30.0f);
				Effect_DoF_FadeFocus(mlDoFEffectID, 0, 12, 30.0f);
				Effect_ImageTrail_FadeOut(mlCurrentImageTrailEffectID, 60.0f);
				
				Effect_Fade_In(0.5f);
				Effect_Flash_Start(0.25, 0.25, 0.5);
				Player_FadeFOVMulTo(1.0f, 25.0f);
				Effect_Screen_FadeOut(mlid_ScanLines, 5.0f);
				
				Effect_Rumble_Start(0.5, 0.7, 0.25f);
				
				StressHeartBeep(1.0f, 0.25f, 1, -1, true);
			}
			else if (Sequence_DoStepAndWait(2.0f)) // 3.0f
			{
				//////////////////////////////////////////
				// Lift the helmet
				Sound_FadeGlobalVolume(1.0f, 3.0f, eSoundEntryType_GuiWorld);
				Sound_FadeGlobalVolume(1.0f, 6.0f, eSoundEntryType_WorldAll);
				Prop_MoveLinearTo("chair_scan_helmet", "chair_scan_helmet_target_1", 0.35, 1, 0.25, true, "MoveTo_ScanHelmet1");
				Prop_AlignRotation("chair_scan_helmet","chair_scan_helmet_target_1",0.35,1,0.25,true,"");
			}
			else if (Sequence_DoStepAndWait(1.0f))
			{
				Sound_PlayGui("04_03_tau_escape/SFX/ross/conversation/tunnel/start", 0.3f);
			}
			else if (Sequence_DoStepAndWait(0.0f))
			{
				Dialog_Begin("Simulation");
					Dialog_AddBranch("3_AliceScanRoom");
						Dialog_AddSubject("3_AliceScanRoom_1", "SimCallback_AliceScanRoom");
						Dialog_AddSubject("3_AliceScanRoom_2", "SimCallback_AliceScanRoom");
						//Dialog_AddSubject("3_AliceScanRoom_3", "SimCallback_AliceScanRoom");
				Dialog_End();
			}
			Sequence_End();
		}
		
		//-------------------------------------------------------
		
		bool EventGettingUp(const tString &in asEntity, int alDirection)
		{
			if (alDirection == 1)
			{
				cLux_AddDebugMessage("**EVENT GETTING UP**");
				
				Entity_SetActive(msSimBrandon, false);
			}
			return true;
		}
			
		//-------------------------------------------------------
		
		void MoveTo_ScanHelmet1(const tString &in asProp)
		{
			if (mbHelmetPhaseTwoStarted) return;
			
			Prop_MoveLinearTo("chair_scan_helmet", "chair_scan_helmet_target_2", 1, 1, 0.25, true, "MoveTo_ScanHelmet1");
			Prop_AlignRotation("chair_scan_helmet","chair_scan_helmet_target_2",0.35,1,0.25,true,"");
			mbHelmetPhaseTwoStarted = true;
		}
		
		//-------------------------------------------------------
		
		bool mbStandUpSoundTriggered = false;
		bool mbNervousBreathingPlaying = false;
		bool StandUpSound(const tString &in asEntity, int alDirection)
		{
			if (asEntity == "IntroAnim_1" && alDirection == 1)
			{
				cLux_AddDebugMessage("Stand Up Animation Sound Start");
				if(mbStandUpSoundTriggered==false)
				{
					Sound_CreateAtEntity("StandUpAnimation", "01_01_upsilon_awake/sfx/wake_up/stand_up_chair", "Player", 0.0f, false, 1.0f);	
					mbStandUpSoundTriggered = true;
				}
				
				if(mbNervousBreathingPlaying == false)
				{
					Emotion_PlayEventBreath("01_01_upsilon_awake/sfx/wake_up/nervous", 0);
					Map_AddTimer("nervousbreathing", 12.7, "timer_nervousbreathing");
					mbNervousBreathingPlaying = true;
				}

				return true;
			}
			
			else if (asEntity == "IntroAnim_*" && alDirection == -1)
			{
				cLux_AddDebugMessage("Stand Up Animation Sound Stop");
				Sound_Stop("StandUpAnimation", 0.25f);
				mbStandUpSoundTriggered = false;
				return true;
			}
			return true;
		}
		
		//-------------------------------------------------------
		
		void timer_nervousbreathing(const tString &in asTimer)
		{
			Map_AddTimer("nervousbreathing", 13.0, "timer_nervousbreathing");
			Emotion_PlayEventBreath("01_01_upsilon_awake/sfx/wake_up/nervous", 0);
		}
		
		//-------------------------------------------------------
		
		/////////////////////////////////////////
		// Dialogue with Alice
		//////////////////////////////////////////
		
		void TimerBrandonSetMainAnimation(const tString& in asAnimation)
		{
			NPC_SetMainAnimation(msSimBrandon, asAnimation);
		}
		
		//-------------------------------------------------------
		
		void TimerBrandonPlayExtraAnimation(const tString& in asAnimation)
		{
			NPC_PlayExtraAnimation(msSimBrandon, asAnimation);
		}
		
		//-------------------------------------------------------
		
		void Line_AliceScanRoomCalm(const tString &in asParent, const tString &in asChild, int alState, bool abStart)
		{
			if (!abStart)
				return;
			NPC_PlayExtraAnimation(msSimAlice, "stand_calm_talking_extra0"+cMath_RandRectl(1,3));
		}
		
		//-------------------------------------------------------
		
		int mlVideoDistortionFlashesID = -1;
		cSequenceStatesData mSequenceFlashesState;
		void SequenceFlashes(const tString &in asTimer)
		{
			Sequence_Begin("SequenceFlashes", mSequenceFlashesState);
			
			if (Sequence_DoStepAndWait(1.0f))
			{
				cLux_AddDebugMessage("!! STARTING FLASHES !!");
				
				mlVideoDistortionFlashesID = Effect_VideoDistortion_Start(0.65f, 3.0f, 1.0f, 1.0f);
				
				Sound_PlayGui("Player/UI/Suit/gui/pressure_alarm_medium", 0.5f);
				
			}
			else if (Sequence_DoStepAndWait(1.0f))
			{
				Sound_StopGui("Player/UI/Suit/gui/pressure_alarm_medium", 0.1f);
				
				StressHeartBeep(0.85f, 0.25f, 1, -1, true);
			}
			Sequence_End();
		}
		
		//-------------------------------------------------------s
		
		void SimCallback_AliceScanRoom(const tString &in asSubject, bool abStartOfSubject)
		{
			if (asSubject=="3_AliceScanRoom_1")
			{
				if (abStartOfSubject)
				{
					Map_AddTimer("sit_calm_extra02",5.0f,"TimerBrandonPlayExtraAnimation");
					Map_AddTimer("sit_calm_extra03",15.0f,"TimerBrandonPlayExtraAnimation");
					Map_AddTimer("sit_calm_extra02",27.0f,"TimerBrandonPlayExtraAnimation");
					Map_AddTimer("sit_calm_extra01",32.0f,"TimerBrandonPlayExtraAnimation");
				}
			}
			else if (asSubject=="3_AliceScanRoom_2")
			{
				if (abStartOfSubject)
				{
					// MUSIC
					Music_Stop(1.0f, eMusicPrio_MinorEvent);
					Music_PlayExt("01_01_thick_air", true, 0.5f, 1.0f, 7.0f, 0.0f, eMusicPrio_BgAmb, true);
					
					PreloadPriorityVeryHigh();
					
					Map_AddTimer("escalation", 3.5f, "Escalation");
					Map_AddTimer("ShutdownSimulation", 8.0f, "ShutdownSimulation");
					Map_AddTimer("ScreenFX", 2.5f, "ScreenFX");
					Map_AddTimer("HeartStressDelay", 4.5f, "HeartStressDelay");
					Map_AddTimer("StandUp", 8.0f, "StandUp");
				}
			}
			else if (asSubject=="3_AliceScanRoom_3")
			{
				if (abStartOfSubject)
				{
					Map_AddTimer("sit_panic",0.1f,"TimerBrandonSetMainAnimation");
					Map_AddTimer("sit_panic_extra02",0.2f,"TimerBrandonPlayExtraAnimation");
					
					cLux_AddDebugMessage("Continue cameraanim");
					
					CameraAnimationNode_SetAutoMove("IntroAnim_1", 1);
					CameraAnimationNode_SetAutoMove("IntroAnim_2", 1);
				}
				else
				SequenceStates_Resume(msCurrentSequence);
			}
		}
		
		//-------------------------------------------------------
		
		void StandUp(const tString &in asTimer)
		{
			Map_AddTimer("sit_panic",0.1f,"TimerBrandonSetMainAnimation");
			Map_AddTimer("sit_panic_extra02",0.2f,"TimerBrandonPlayExtraAnimation");
					
			cLux_AddDebugMessage("Continue cameraanim");
					
			CameraAnimationNode_SetAutoMove("IntroAnim_1", 1);
			CameraAnimationNode_SetAutoMove("IntroAnim_2", 1);
		}
		
		//-------------------------------------------------------
		
		void ShutdownSimulation(const tString &in asTimer)
		{
			// SET POWER OFF
			//Sound_PlayGui("02_06_theta_tunnels/sweeteners/lights_out", 1.0f);
			//Sound_PlayGui("02_06_theta_tunnels/sweeteners/lights_out_room_shake", 1.0f);
			
			Sound_FadeGlobalVolume(0.0f, 0.1f, eSoundEntryType_GuiWorld);
			Sound_FadeGlobalVolume(0.0f, 0.75f, eSoundEntryType_WorldAll);
			
			// EFFECTS
			Effect_Fade_Out(0.5f);
			Effect_Flash_Start(0.25, 0.25, 0.5);
			Player_FadeFOVMulTo(1.75f, 25.0f);
			
			Effect_Rumble_Start(0.9, 0.7, 0.25f);
			Effect_Shake_Start(0.08,6.75,1.0,0.1);
			
			Emotion_StopHeartbeat(mlHeartBeatID);
			mbStressTrackerActive = false;
			
			Sound_StopGui("04_03_tau_escape/SFX/ross/conversation/tunnel/loop", 0.1f, false);
			Sound_PlayGui("04_03_tau_escape/SFX/ross/conversation/tunnel/stop", 0.2f);
			
			Sound_StopGui("Player/UI/Suit/gui/pressure_alarm_medium", 0.1f);
			
			// MUSIC IMPACT
			Music_PlayOverlay("01_01_construct_smashing_about", 0.25f);
			Map_AddTimer("ChangeMap", 1.0f, "ChangeMap");
		}
		
		//-------------------------------------------------------
		
		void ChangeMap(const tString &in asTimer)
		{
			Player_ChangeStateToNormal();
			Player_SetHealth(0.35f);
			
			Effect_ImageTrail_FadeOut(mlCurrentImageTrailEffectID, 0);
//			Effect_RadialBlur_FadeOut(mlRadialBlurID, 0);
			Effect_DoF_FadeOut(mlDoFEffectID, 0);
			
			Effect_Screen_FadeOut(mlid_ScanLines, 0.0);
			
			/////////////////////////
			// Change map
			cLux_AddDebugMessage("Switching scene...");
			
			LoadScreen_SetUseSmallIcon(false);
			Map_ChangeMap("00_01_maintenance.hpm", "PlayerStartArea_1", "", "");
		}
		
		//-------------------------------------------------------
		
		void Escalation(const tString &in asTimer)
		{
			Sound_PlayGui("03_02_omicron_inside/SFX/escalation/escalation_event_01", 0.33);
		}
		
		//-------------------------------------------------------
		
		int mlid_ScanLines;
		
		void ScreenFX(const tString &in asTimer)
		{
			Effect_VideoDistortion_Start(.85f, 2.0f, 0.1f, 0.1f);
			
			mlImageTrailID = Effect_ImageTrail_Start(1.0, 5.5f, -1, 2);
			mlRadialBlurID = Effect_RadialBlur_Start(0.5f, 0.55f, 0.35f, 5.5f);
			mlDoFEffectID = Effect_DoF_Start(4.0f, 8.0f, 0.5f, 5.5f);
			
			// FADE IN SCANLINES
			mlid_ScanLines = Effect_Screen_Start("00_04_scan_noise.mat", cVector2f(0.5, 0.5), cVector2f(2, 1),  10.0f, 0.1, 900, 0.1);
			Effect_Screen_FadeAlpha(mlid_ScanLines, 0.0f, 0);
			Effect_Screen_FadeAlpha(mlid_ScanLines, 1.0f, 3.0f);
			Sound_CreateAtEntity("PlayerTransfer", "Entities_Special/Equipment/scanner/urban/scan_sequence", "Player", 0, true);
		}
		
		void HeartStressDelay(const tString &in asTimer)
		{
			mlHeartBeatID = StressHeartBeep(0.65f, 0.4f, 1, 6.0f, true, 5.0f, 1.0f);
			Sound_PlayGui("Player/UI/Suit/gui/pressure_alarm_medium", 0.5f);
		}
		
		//-------------------------------------------------------
		
		//} End Brandon wakes up in the scan chair
		
	//} END SCENE 1
	
	/////////////////////////////////////////
	// PRELOADING
	//{///////////////////////////////////////

	//-------------------------------------------------------

	bool PreloadDisabled(bool abCheckIfPreloading = true)
	{
		if(cLux_ScriptDebugOn())
		{
			cLux_AddDebugMessage("PRELOAD DISABLED!"); 
			
			return true;
		}
		
		if(abCheckIfPreloading && Map_IsPreloading() == false)
			return true;
			
		return false;
	}
	
	//-------------------------------------------------------
	
	void TimerBeginPreload(const tString &in asTimer)
	{
		Preload();
	}
	
	//-------------------------------------------------------
	
	//////////////////////
	//Begin preload.
	void Preload()
	{
		if(PreloadDisabled(false)) return;
		cLux_AddDebugMessage("Preloading");
		
		Map_Preload("00_01_maintenance.hpm", eWorldStreamPriority_Low);
	}
	
	//-------------------------------------------------------
	
	//////////////////////
	//Change preload prio to normal
	void PreloadPriorityLow()
	{
		if(PreloadDisabled()) return;
		
		Map_SetPreloadPriority(eWorldStreamPriority_Low);
	}	
	
	//-------------------------------------------------------
	
	//////////////////////
	//Change preload prio to normal
	void PreloadPriorityNormal()
	{
		if(PreloadDisabled()) return;
		
		Map_SetPreloadPriority(eWorldStreamPriority_Normal);
	}
	
	//-------------------------------------------------------
	
	//////////////////////
	//Change preload prio to normal
	void PreloadPriorityVeryHigh()
	{
		if(PreloadDisabled()) return;
		
		Map_SetPreloadPriority(eWorldStreamPriority_VeryHigh);
	}
	
	/////////////////////////////////////////
	// ==============
	// TERMINALS
	// ==============
	//{//////////////////////////////////////

		//-------------------------------------------------------
	
		/////////////////////////////////////////
		// Terminal *TERMINAL X*
		//{//////////////////////////////////////
		
		//-------------------------------------------------------
	
		/*Put any variables that are only used Terminal here.*/
	
		//-------------------------------------------------------
	
		/*Put any functions that are only used Terminal here.*/
	
		//-------------------------------------------------------
	
		//} END Terminal *Name Of Terminal*
	
	//} END TERMINALS
	
	/////////////////////////////////////////////////////////////////////////////////////////
	// ==============
	// AUDIO
	// ==============
	//{//////////////////////////////////////////////////////////////////////////////////////
	
	//-------------------------------------------------------
	
	//-------------------------------------------------------
	
	//} END AUDIO
}